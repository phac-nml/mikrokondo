nextflow_pipeline {

    name "Full Integration Tests for MIKROKONDO"
    script "main.nf"

    test("Should run without failures") {

        when {
            params {
                input = "https://github.com/phac-nml/mikrokondo/raw/dev/tests/data/samplesheets/samplesheet-campy-staph.csv"
                outdir = "results"

                platform = "illumina"

                mash { mash_sketch = "https://github.com/phac-nml/mikrokondo/raw/dev/tests/data/databases/campy-staph-ecoli.msh" }
                r_contaminants { mega_mm2_idx = "https://github.com/phac-nml/mikrokondo/raw/dev/tests/data/databases/campy.mmi" }

                skip_bakta = true
                skip_staramr = true
                skip_mobrecon = true
                skip_checkm = true
                skip_raw_read_metrics = true
                skip_polishing = true

                max_memory = "2.GB"
                max_cpus = 1
            }
        }

        then {
            assert workflow.success
            assert path("$launchDir/results").exists()

            // parse output json file
            def json_string = path("$launchDir/results/SummaryReport/final_report.json").readLines().join("\n")
            def json_parser = new groovy.json.JsonSlurper()
            def json = json_parser.parseText(json_string)

            assert json.CSE.CSE.FastP.summary.sequencing.equals("paired end (150 cycles + 150 cycles)")
            assert json.CSE.CSE.FastP.summary.before_filtering.total_reads.equals(248)
            assert json.CSE.CSE.FastP.filtering_result.passed_filter_reads.equals(248)
            assert json.CSE.CSE.FastP.insert_size.peak.equals(196)
        }

    }

}
